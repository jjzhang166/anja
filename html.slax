version 1.2;

mvar $chapter_counter = 0;

key cite_id {
    match /document/bibliography/item;
    value @id;
}

key Listing {
    match /document/listing;
    value @id;
}

match * {
    copy-of .;
}

match includegraphics {
    <source> {
        attribute "media" {
            expr @media;
        }
        attribute "srcset" {
            expr @src;
        }
    }
}

match titlepic {
    <picture> {
        apply-templates;
        <img> {
            attribute "src" {
                expr includegraphics[last()]/@src;
            }
        }
    }
}

match ref {
    <a> {
        attribute "href" {
            expr "#";
            expr .;
        }
        expr @what;
        expr " ";
        expr count(key(@what, .)/preceding-sibling::chapter);
        expr ".";
        expr count(key(@what, .)/preceding-sibling::listing) + 1;
    }
}

match env {
    <code class="env"> {
        copy-of node();
    }
}

match prgname {
    <code class="prgname"> {
        copy-of node();
    }
}

match dirname {
    <code class="dirname"> {
        copy-of node();
    }
}

match caption {
    apply-templates;
}

match code {
    <pre> {
        <code> {
            attribute "class" {
                expr @language;
            }
            copy-of node();
        }
    }
}

match listing {
    <div class="listing"> {
        attribute "id" {
            expr @id;
        }
        <p> {
            <strong> {
                expr "Listing ";
                number {
                    level "any";
                    count chapter;
                }
                expr ".";
                number {
                    level "any";
                    from chapter;
                }
            }
            expr ": ";
            apply-templates caption;
        }
        apply-templates code;
    }
}

match libname {
    <code> {
        copy-of node();
    }
}

match quantity {
    expr .;
    expr " ";
    expr ./@unit;
}

match title { }

match author { }

match cite {
    expr " ";
    <cite> {
        <a> {
            attribute "href" {
                expr "#";
                expr .;
            }
            expr "[";
            expr count(key("cite_id", .)/preceding-sibling::*) + 1;
            expr "]";
        }
    }
}

match p {
    <p> {
        apply-templates;
    }
}

match abstract {
    <div class="abstract"> {
        copy-of node();
    }
}

match chapter {
    mode "toc";
    <li> {
        <a> {
            attribute "href" {
                expr "#";
                expr @id;
            }
            copy-of node();
        }
    }
}

match chapter {
    append $chapter_counter += 1;
    <h2> {
        attribute "id" {
            expr @id;
        }
        $chapter_counter;
        number;
        <span class="fill">;
        copy-of node();
    }
}

match section {
    mode "toc";

    <li> {
        <a> {
            attribute "href" {
                expr "#";
                expr @id;
            }
            copy-of node();
        }
    }
}

match section {
    <h3> {
        attribute "id" {
            expr @id;
        }
        number {
            level "any";
            count chapter;
        }
        expr ".";
        number {
            level "any";
            from chapter;
        }
        <span class="fill">;
        copy-of node();
    }
}

match subsection {
    mode "toc";

    <li> {
        <a> {
            attribute "href" {
                expr "#";
                expr @id;
            }
            copy-of node();
        }
    }
}

match subsection {
    <h4> {
        attribute "id" {
            expr @id;
        }
        number {
            level "any";
            count chapter;
        }
        expr ".";
        number {
            level "any";
            from chapter;
        }
        expr ".";
        number {
            level "any";
            from chapter;
        }
        <span class="fill">;
        copy-of node();
    }
}

match tableofcontents {
    <nav id="toc"> {
        <h2> "Table of contents";
        <ol> {
            apply-templates ../chapter {
                mode "toc";
            }
        }
    }
}

match url {
    <a> {
        attribute "href" {
            expr .;
        }
        expr .;
    }
}

match item {
    <p class="bibitem"> {
        attribute "id" {
            expr @id;
        }
        expr "\n[";
        number {
            level "single";
        }
        expr "]";
        <span class="fill">;
        apply-templates;
    }
}

match bibliography {
    <h2> "References";
    apply-templates;
}

match document {
    <html> {
        <head> {
            <title> title;
            <link rel="stylesheet" href="highlight/styles/default.css" type="text/css">;
            <link rel="stylesheet" href="format.css" type="text/css">;
            <script src="highlight/highlight.pack.js" type="text/javascript">;
            <script type="text/javascript"> "hljs.initHighlightingOnLoad();";
        }
        <body> {
            <h1> title;
            <p class="titlepage"> author;
            apply-templates;
        }
    }
}

main {
    apply-templates;
}
